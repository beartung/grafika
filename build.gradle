//from http://www.egeek.me/2013/12/07/gradle-auto-detect-android-sdk-and-build-tools-versions/
import org.codehaus.groovy.runtime.StackTraceUtils

int[] sdksAvailable() {
    def sdks = new ByteArrayOutputStream()
    def androidSdkDir = androidSDKDir()
    exec {
        workingDir androidSdkDir + "/tools"
        commandLine './android', 'list'
        standardOutput = sdks
    }
    sdks = sdks
            // get the output
            .toString()
            // split to strings
            .split('\n')
            // leave only strings with API levels
            .findAll { it ==~ /\s*API level:\s*(\d+).*/ }
            // extract the API levels
            .collect { (it =~ /\s*API level:\s*(\d+).*/)[0][1].toInteger() }
            // sort from highest to lowest
            .sort( { a, b -> b <=> a } )
    sdks
}

int highestSdkAvailable(int defaultSdk) {
    try {
        def sdks = sdksAvailable()
        def highestSdk = sdks[0]
        if (highestSdk != null) {
            println "Using highest found SDK " + highestSdk
            highestSdk
        } else {
            println "No installed SDKs found. Using default SDK " + defaultSdk
            defaultSdk
        }
    } catch (any) {
        println "Exception while determining highest SDK. Using default SDK " +
                defaultSdk
        StackTraceUtils.sanitize(any).printStackTrace()
        defaultSdk
    }
}

String androidSDKDir() {
    def androidExecPath = new ByteArrayOutputStream()
    try {
        exec {
            commandLine 'which', 'android'
            standardOutput = androidExecPath
        }
        println "find android exec path: " + androidExecPath.toString()
        file(androidExecPath.toString().trim()).getParentFile().getParentFile()
    } catch (ExecException) {
        exec {
            commandLine 'grep', 'sdk.dir', 'local.properties'
            standardOutput = androidExecPath
        }
        println "find android sdk with local.properties: " + androidExecPath.toString()
        file(androidExecPath.toString().trim().split('=')[1])
    }
}

String[] buildToolsAvailable() {
    println "sdk dir: " + androidSDKDir()
    def buildToolsDir = new File(androidSDKDir(), "build-tools")
    buildToolsDir.list([accept:{d, f-> !f.contains("android") }] as FilenameFilter).sort { a, b -> b <=> a }
}

String latestBuildToolsAvailable(String defaultBuildTools) {
    try {
        def buildToolsVersions = buildToolsAvailable()
        def latestBuildTools = buildToolsVersions[0]
        if (latestBuildTools != null) {
            println "Using latest found build tools " + latestBuildTools
            latestBuildTools
        } else {
            println "No installed build tools found. Using default build tools " +
                    defaultBuildTools
            defaultBuildTools
        }
    } catch (any) {
        println "Exception while determining latest build tools. Using default build tools " +
                defaultBuildTools
        StackTraceUtils.sanitize(any).printStackTrace()
        defaultBuildTools
    }
}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${ANDROID_GRADLE_PLUGIN_VERSION}"
        classpath "de.undercouch:gradle-download-task:${GRADLE_DOWNLOAD_TASK_VERSION}"
    }
}

allprojects {

    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }

    ext {
        buildToolsVersion = latestBuildToolsAvailable("${BUILD_TOOLS_VERSION}")
        compileSdkVersion = highestSdkAvailable(COMPILE_SDK_VERSION.toInteger())
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

}


apply plugin: 'com.android.application'

android {
    buildToolsVersion rootProject.ext.buildToolsVersion
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        multiDexEnabled true
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            //jniLibs.srcDirs = ['libs']
            //jni.srcDirs = []
        }
    }

    buildTypes {
        debug {
            jniDebuggable true
            debuggable true
            zipAlignEnabled false
        }
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile "com.android.support:support-v4:${SUPPORT_V4_VERSION}"
    compile fileTree(dir: 'libs', include: ['*.jar'])
}
